Class Port.SourceControl.Installer [ Abstract ]
{

ClassMethod Install() As %Status
{
  set sc = $$$OK
  
  try {
    write !, "Installing Port ... please wait a moment.", !
    write "> Creating global mappings ...", !
    $$$ThrowOnError(..CreateGlobalMapping())
    write "> Setting up workspace structure ... ", !
    do ##class(Config).SetExportPath("/CacheProjects/{NAMESPACE}/{PROJECT}")
    write "> Setting up test files directory ...", !
    do ##class(Config).SetTestPath("tests/{PROJECT}")
    write "> Defining default test class prefix ..", !
    do ##class(Config).SetTestClassPrefix("UnitTest")
    write "> Enabling auto-testing ...", !
    do ##class(Config).EnableTestOnDemand()
    write "> Enabling auto-export for project XML ...", !
    do ##class(Config).EnableAutoExportXML()
    write "> Setting up log level ...", !
    do ##class(Config).SetLogLevel(1)
    write "> Setting up source extension ...", !
    do ##class(Config).SetSourceExtension("txt")    
    write "> Registering LogCleaner task ...", !
    $$$ThrowOnError(##class(LogCleaner).StartTask())
    write "> Activating source control class ...", !!
    $$$ThrowOnError(##class(%Studio.SourceControl.Interface).SourceControlClassSet("Port.SourceControl.Hooks", $namespace))
    write "ALL DONE.", !
    write "NOTE: Please note that you must restart Studio in order to make the new settings have effect."
  } catch ex {
    set sc = ex.AsStatus()
    write !!, "FATAL: Failed to install port due to the following reason: "
    do $System.OBJ.DisplayError(sc)
  }
  quit sc
}

ClassMethod CreateGlobalMapping() As %Status
{
  set sc = $$$OK
  set thisNamespace = $namespace     
  new $namespace
  
  set $namespace = "%SYS"
  
  try {
    $$$ThrowOnError(##class(Config.Namespaces).Get(thisNamespace, .nsProperties))
    set glProperties("Database") = nsProperties("Globals")
    $$$ThrowOnError(##class(Config.MapGlobals).Create(thisNamespace, "Port.SourceControl.Projects", .glProperties))
    $$$ThrowOnError(##class(Config.MapGlobals).Create(thisNamespace, "Port.SourceControl.Settings", .glProperties))
    set $namespace = thisNamespace
  } catch ex {
    set $namespace = thisNamespace
    set sc = ex.AsStatus()
  }
  quit sc
}

}

